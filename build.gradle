plugins {
    id 'java'
    id 'maven-publish'

    id 'eclipse'
    id 'idea'

    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.jfrog.bintray' version '1.2'
}

version = '0.1'
description = 'TODO'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    apply plugin: 'license'
}

allprojects {
    group = 'blue.lapis'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        jcenter()
    }

    license {
        header rootProject.file('LICENSE')
        include '**/*.java'
        include '**/*.groovy'

        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar

                repositories {
                    maven {
                        url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                    }
                }
            }
        }
    }
}

subprojects {
    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
    }

    eclipse {
        classpath {
            plusConfigurations += [configurations.provided]
        }
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    dependencies {
        provided(rootProject) {
            transitive = false
        }
    }
}

dependencies {
    compile 'com.google.guava:guava:17.0'
    compile 'org.ow2.asm:asm:5.0.3'
    compile 'org.slf4j:slf4j-api:1.7.12'
}

bintray {
    if (project.hasProperty('bintrayUser')) {
        user = project.bintrayUser
        key = project.bintrayKey
    }

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'lapis'
        desc = project.description
        licenses = ['MIT']

        websiteUrl = 'https://github.com/LapisBlue/MethodRemapper'
        issueTrackerUrl = "$websiteUrl/issues"
        vcsUrl = "${websiteUrl}.git"

        labels = ['remapper', 'asm']

        publicDownloadNumbers = true

        version {
            name = project.version
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
