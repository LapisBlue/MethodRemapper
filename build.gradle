plugins {
    id 'java'
    id 'maven-publish'

    id 'eclipse'
    id 'idea'

    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.jfrog.bintray' version '1.2'
}

group = 'blue.lapis'
version = '0.1-SNAPSHOT'
description = 'TODO'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:17.0'
    compile 'org.ow2.asm:asm:5.0.3'
    compile 'org.slf4j:slf4j-api:1.7.12'
}

license {
    header file('LICENSE')
    include '**/*.java'

    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                }
            }
        }
    }
}

bintray {
    if (project.hasProperty('bintrayUser')) {
        user = project.bintrayUser
        key = project.bintrayKey
    }

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'lapis'
        desc = project.description
        licenses = ['MIT']

        websiteUrl = 'https://github.com/LapisBlue/MethodRemapper'
        issueTrackerUrl = "$websiteUrl/issues"
        vcsUrl = "${websiteUrl}.git"

        labels = ['remapper', 'asm']

        version {
            name = project.version
            vcsTag = name
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
